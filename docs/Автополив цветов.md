
Наши квартиры и дома не обходятся без цветов и растений, ведь они украшают, улучшают воздух и делают жилище более комфортным. Однако, за ними требуется уход - полив, в разное время и разным количеством воды. Это отличная область применения домашней автоматизации.

Часто такие решения делают полностью автоматическими. Проблема их в том, что они должны быть довольно сложными, чтобы качественно справляться с этой работой: не поливать ночью, легко настраиваться, учитывать контекст и т.п.

Куда выгоднее всю логику вынести в контроллер и настроить его заботиться о вашем растении. Итак, для этого вам потребуются следующие компоненты.

|Название|Назначение|Цена, руб.|
| :----------- |:----------- |:----------- |
|[BLE JDY-08](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FBluetooth-4-0-BLE-Low-Power-CC2541-JDY-08-Support-Airsync-iBeacon-Module%2F322511962233%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Радиомодуль Bluetooth LE|140|
|[Arduino Pro Mini](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F2PCS-New-Pro-Mini-atmega328-Board-5V-16M-Arduino-Compatible-Nano%2F191674251828%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Микроконтроллер для снятия показаний датчика|135|
|[YL-38](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F5PCS-Soil-Hygrometer-Detection-Module-Soil-Moisture-Sensor-For-arduino-Smart-car%2F400385860375%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Датчик влажности почвы|40|
|[Breadboard 170](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F5-Color-Mini-Solderless-Prototype-Breadboard-170-Tie-points-For-Arduino-Shield%2F201677166530%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Небольшая макетка для компоновки модулей датчика|30|
|[Помпа DC 3-6v 120L/H](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FUltra-quiet-Mini-DC-3-6V-120L-H-Brushless-Motor-Submersible-Water-Pump-New%2F201559004919%3F_trkparms%3Daid%253D222007%2526algo%253DSIM.MBE%2526ao%253D2%2526asc%253D20131003132420%2526meid%253Dfd6ec8f766314da0a7ba4fc97ffa719b%2526pid%253D100005%2526rk%253D6%2526rkt%253D6%2526mehot%253Dpp%2526sd%253D311588371634%2526itm%253D201559004919%26_trksid%3Dp2047675.c100005.m1851)|Помпа для полива|85|
|[IRF520](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F5PCS-IRF520N-IRF520-Power-MOSFET-N-Channel-TO-220%2F401073473279%3Fhash%3Ditem5d61d786ff%3Ag%3ABPoAAOSwkNZUc~Kn)|Полевой транзистор для управления помпой|16|
|[Держатель батареек 3x AAA](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F2-3-4xAAA-Battery-Holder-Plastic-Batteries-Box-Battery-Storage-Case-With-Wire-TS%2F152748115174%3Fhash%3Ditem23907f44e6%3Am%3AmR_En8kk8yOo9vvMl-QhG0g)|Можно было бы обойтись и двумя батарейками, если подобрать подходящий полевой транзистор. IRF520 переключается напряжением выше 4В.|80|
|Капельница|Можно купить в любой аптеке|15|
|Цветочный горшок|Высокий горшок с вкладышем, чтобы организовать резервуар с водой|200|

Из этого набора элементов вы можете собрать устройство, похожее на что-то подобное:

![Цветочный горшок с автополивом](https://github.com/cutecare/cutecare-docs/blob/master/images/FlowerAutoWatering.jpg?raw=true)

В режиме ожидания устройство потребляет 0.1 мА. В режиме полива - 60 мА, из-за прожорливости помпы. Время полива может составлять около 5 сек. Полив осуществляется раз в день или реже. 3-х элементов питания LR03 должно хватить на несколько месяцев.

Чтобы заполнить резервуар водой, достаточно вынуть из земли датчик влажности, вынуть вкладыш с растением и залить в горшок воду. Кстати, штатных возможностей JDY-08 хватит на то, чтобы добавить еще несколько датчиков, например, уровня воды в резервуаре, температуры и еще чего-нибудь.

В данном решении объединены прототип [датчика влажности](http://cutecare.readthedocs.io/ru/master/%D0%A1%D0%B5%D0%BD%D1%81%D0%BE%D1%80%20CC41A/) и прототип [реле](http://cutecare.readthedocs.io/ru/master/%D0%A0%D0%B5%D0%BB%D0%B5%20JDY-08/)

*Программируем микроконтроллер*

О том как настроить Arduino IDE и подключить микроконтроллер к ПК читайте в этой коротенькой [инструкции](http://cutecare.readthedocs.io/ru/master/%D0%9C%D0%B8%D0%BA%D1%80%D0%BE%D0%BA%D0%BE%D0%BD%D1%82%D1%80%D0%BE%D0%BB%D0%BB%D0%B5%D1%80%D1%8B/#arduino-pro-mini).

```
#include <SoftwareSerial.h>
#include <LowPower.h>

#define RX_PIN 4
#define TX_PIN 11
#define SENSOR_PWR_PIN 12
#define SENSOR_PIN A2
#define BLE_PIN 10
#define BLE_BAUD 115200

unsigned int lastMoisture = 0;
int secondsPassed = 0;

void setup() 
{
  // configure BLE device
  wakeUpBLE();

  SoftwareSerial bleSerial(RX_PIN, TX_PIN);
  bleSerial.begin(BLE_BAUD);
  sendCommand(&bleSerial, "AT+RESTORE");
  delay(500);
  sendCommand(&bleSerial, "AT+NAMEFlower-1"); 
  sendCommand(&bleSerial, "AT+HOSTEN3");
  sendCommand(&bleSerial, "AT+CLSSE0");
  sendCommand(&bleSerial, "AT+RST");
  sendCommand(&bleSerial, "AT+POWR0");      // max RF power
  sendCommand(&bleSerial, "AT+ADVEN1");     // advertising enabled
  sendCommand(&bleSerial, "AT+ADVIN1");     // setup advertising interval
  sendCommand(&bleSerial, "AT+PWMOPEN0");   // turn off PWM
  sendCommand(&bleSerial, "AT+RTCOPEN0");   // disable RTC
  sendCommand(&bleSerial, "AT+STARTEN0");   // start in sleep mode

  measureSensor(&lastMoisture);
  sendSensorData(&bleSerial, lastMoisture);
  pinMode(TX_PIN, INPUT);
}

void loop() 
{
  // fall a sleep
  LowPower.powerDown(SLEEP_8S, ADC_OFF, BOD_OFF);

  // check if 5 minutes have passed
  secondsPassed += 8;
  if ( secondsPassed < 300 ) return;
  secondsPassed = 0;

  // measure sensor value
  unsigned int nowMoisture = 0;
  measureSensor(&nowMoisture);
  if ( nowMoisture == lastMoisture ) return;
  lastMoisture = nowMoisture;

  // configure BLE if sensor values have changed since the last measure
  SoftwareSerial bleSerial(RX_PIN, TX_PIN);
  bleSerial.begin(BLE_BAUD);
  sendSensorData(&bleSerial, lastMoisture);
  pinMode(TX_PIN, INPUT);
}

void sendSensorData( SoftwareSerial * bleSerial, int moisture ) 
{
  char buff[32] = "";
  sprintf(buff, "AT+HUMID%02X", moisture);

  wakeUpBLE();
  sendCommand(bleSerial, buff);
  sendCommand(bleSerial, buff);
  sendCommand(bleSerial, "AT+SLEEP1");
}

void measureSensor(unsigned int * value) 
{
  pinMode(SENSOR_PWR_PIN, OUTPUT);
  digitalWrite(SENSOR_PWR_PIN, HIGH);
  delay(200);
  *value = 100 - (int) (((float) analogRead(SENSOR_PIN) / 1024) * 100);
  digitalWrite(SENSOR_PWR_PIN, LOW);
  pinMode(SENSOR_PWR_PIN, INPUT);
}

void sendCommand(SoftwareSerial * bleSerial, const char * data) {
  delay(200);
  bleSerial->print(data);
}

void wakeUpBLE()
{
  pinMode(BLE_PIN, OUTPUT);
  digitalWrite(BLE_PIN, HIGH);
  delay(100);
  digitalWrite(BLE_PIN, LOW);
  pinMode(BLE_PIN, INPUT);
}
```

*Собираем схему*

![Схема умного цветочного горшка](https://github.com/cutecare/cutecare-docs/blob/master/images/FlowerWatering_bb.png?raw=true)

*Программируем HASS*
