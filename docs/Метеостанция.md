[Открытая платформа для создания заботливой квартиры](http://cutecare.ru)

Измерение различных параметров среды обитания, позволяют реализовывать самые разные сценарии:

* Температуру в помещении можно использовать для автоматического включения/отключения нагревателей или теплого пола.
* Атмосферное давление можно использовать для прогнозирования осадков или выявления неблагоприятных условий для метеозависимых людей.
* Уровень газа (например, CO2) для сигнала о необходимости проветрить комнату и т.д.

Итак, для сборки собственной метеостанции вам потребуются эти компоненты:

|Название|Назначение|Цена, руб.|
| :----------- |:----------- |:----------- |
|[BLE JDY-08](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FBluetooth-4-0-BLE-Low-Power-CC2541-JDY-08-Support-Airsync-iBeacon-Module%2F322511962233%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Радиомодуль Bluetooth LE|140|
|[Arduino Nano](https://www.ebay.com/itm/Nano-V3-0-ATmega328P-CH340G-5V-16M-MINI-USB-Nano-V3-0-Compatible-to-Arduino/253141071056?ssPageName=STRK%3AMEBIDX%3AIT&_trksid=p2057872.m2749.l2649)|Микроконтроллер для снятия показаний датчика и управления реле|180|
|[MPL3115A2](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FMPL3115A2-IIC-I2C-Intelligent-Temperature-Pressure-Altitude-Sensor-For-Arduino%2F401347699449%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Датчик температуры, давления, высоты|170|
|[MQ-135](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F2PCS-MQ135-MQ-135-Air-Quality-Sensor-Hazardous-Gas-Detection-Module-Arduino%2F181846910843%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Электрохимический датчик CO2|90|

Еще вам потребуются перемычки и макетные платы. Устройство подключается через обычную зарядку для телефона к сети 220В.

**Программируем микроконтроллер**

О том как настроить Arduino IDE и подключить микроконтроллер к ПК читайте в этой коротенькой [инструкции](http://cutecare.readthedocs.io/ru/master/%D0%9C%D0%B8%D0%BA%D1%80%D0%BE%D0%BA%D0%BE%D0%BD%D1%82%D1%80%D0%BE%D0%BB%D0%BB%D0%B5%D1%80%D1%8B/#arduino-nano).
Для компиляции программы необходимо загрузить библиотеки [MPL3115A2](https://github.com/adafruit/Adafruit_MPL3115A2_Library/archive/master.zip) и [JDY8](https://github.com/cutecare/jdy8/archive/master.zip), добавить их в Arduino IDE через меню Sketch - Install library.

```
#include <jdy8.h>
#include <SoftwareSerial.h>
#include <LowPower.h>
#include <Adafruit_MPL3115A2.h>

#define SENSOR_VCC_PIN 4
#define SENSOR_DATA_PIN A6

JDY8 ble(11, 7, 10); // RX, TX, Reset
unsigned int pressure = 0;
unsigned int temperature = 0;
unsigned int gaslevel = 0;
int secondsPassed = 0;

void setup() 
{
  measureValues(&pressure, &temperature, &gaslevel);
  ble.configure("Climate-1");
  ble.setData(pressure, gaslevel, 0, temperature, 0);
}

void loop() 
{
  LowPower.powerDown(SLEEP_8S, ADC_OFF, BOD_OFF);

  // check if 5 minutes have passed
  secondsPassed += 8;
  if ( secondsPassed < 300 ) return;
  secondsPassed = 0;

  // measure sensor value
  measureValues(&pressure, &temperature, &gaslevel);
  ble.setData(pressure, gaslevel, 0, temperature, 0);
}

void measureValues(unsigned int * pressure, unsigned int * temperature, unsigned int * gaslevel) 
{
  // read temperature and pressure
  Adafruit_MPL3115A2 sensor;
  sensor.begin();
  float barCoeff = 0.00750063755419211; // millimeter of mercury [0 °C]
  *pressure = sensor.getPressure() * barCoeff;
  *temperature = sensor.getTemperature();
  pinMode(A4, INPUT); // SDA
  pinMode(A5, INPUT); // SCL

  // read gas level  
  pinMode(SENSOR_VCC_PIN, OUTPUT);
  digitalWrite(SENSOR_VCC_PIN, HIGH);
  pinMode(SENSOR_DATA_PIN, INPUT);
  *gaslevel = analogRead(SENSOR_DATA_PIN);
  digitalWrite(SENSOR_VCC_PIN, LOW);
  pinMode(SENSOR_VCC_PIN, INPUT);
}
```

**Собираем по схеме**

![Схема умной метеостанции](https://github.com/cutecare/cutecare-docs/blob/master/images/Meteostation_bb.png?raw=true)

**Программируем HASS**

Файл: /config/configuration.yaml

```
sensor:
  - platform: cutecare
    scan_interval: 60
    mac: 0C:B2:B7:3E:C8:DC
    monitored_conditions:
      - temperature
      - pressure
      - co2
    name: climate_1
```

Файл: /config/customize.yaml

```
sensor.climate_1_temperature:
  friendly_name: Температура
  icon: mdi:thermometer

sensor.climate_1_pressure:
  friendly_name: Атмосферное давление
  icon: mdi:nature

sensor.climate_1_co2:
  friendly_name: Уровень CO2
  icon: mdi:nature
```
