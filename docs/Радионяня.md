[Открытая платформа для создания заботливой квартиры](http://cutecare.ru)

Чтобы не пропустить момент пробуждения вашего малыша, совсем необязательно покупать дорогую радионяню.
Вы легко можете собрать аналог из стандартных модулей и микрофона, итак, вам потребуется

|Название|Назначение|Цена, руб.|
| :----------- |:----------- |:----------- |
|[BLE CC41-A](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FArduino-Android-IOS-HM-10-BLE-Bluetooth-4-0-CC2540-CC2541-Serial-Wireless-Module%2F311567433651%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Радиомодуль Bluetooth LE|150|
|[Arduino Pro Mini](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F2PCS-New-Pro-Mini-atmega328-Board-5V-16M-Arduino-Compatible-Nano%2F191674251828%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Микроконтроллер для детектирования плача|135|
|[Микрофон](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FMicrophone-Sensor-High-Sensitivity-Sound-Detecte-Voice-switch-Module-For-Arduino-%2F311655372171%3Fhash%3Ditem48901b958b)|Чувствительный микрофон|60|

Запитать устройство можно от двух батареек AA.
Разместить компоненты можно внутри или снаружи какой-то игрушки, чтобы не смущать спящего малыша.

![Радионяня своими руками](https://github.com/cutecare/cutecare-docs/blob/master/images/BabyMonitorDevice.jpg?raw=true)

**Программируем микроконтроллер**

О том как настроить Arduino IDE и подключить микроконтроллер к ПК читайте в этой коротенькой [инструкции](http://cutecare.readthedocs.io/ru/master/%D0%9C%D0%B8%D0%BA%D1%80%D0%BE%D0%BA%D0%BE%D0%BD%D1%82%D1%80%D0%BE%D0%BB%D0%BB%D0%B5%D1%80%D1%8B/#arduino-pro-mini).
Для компиляции программы необходимо загрузить библиотеку [CC41A](https://github.com/cutecare/cc41a/archive/master.zip) и добавить в Arduino IDE через меню Sketch - Install library.

```
#include <cc41a.h>
#include <LowPower.h>

#define SENSOR_PIN A3
#define MAX_NOISE_LEVEL 600
#define NOISE_DURATION_SECS 3

CC41A ble(7, 6, 8); // RX, TX, Reset

int noiseTimeline[12] = {0,0,0,0,0,0,0,0,0,0,0,0};
int mode = 0;
int waitForReset = 0;

void setup() {
  ble.configure("Noise-1");
}

void loop() 
{
  // fall a sleep
  LowPower.powerDown(SLEEP_250MS, ADC_OFF, BOD_OFF);

  delay(50);
  pinMode(SENSOR_PIN, INPUT);
  int sensorValue = analogRead(SENSOR_PIN);

  // store noise history
  for( int i = sizeof(noiseTimeline) / sizeof(int); i > 1; i-- ) {
    noiseTimeline[i - 1] = noiseTimeline[i - 2];
  }
  noiseTimeline[0] = sensorValue > MAX_NOISE_LEVEL ? 1 : 0;

  // calculate times of noise detected
  int noiseTimes = 0;
  for( int i = 0; i < sizeof(noiseTimeline) / sizeof(int); i++ ) {
    noiseTimes += noiseTimeline[i];
  }

  switch( mode ) {
    case 0: // waiting for noise
      if ( noiseTimes >= NOISE_DURATION_SECS ) {
        ble.major(1);
        mode = 1;
        waitForReset = 30;
      }
      break;
    case 1: // waiting for reset
      if ( --waitForReset < 1 ) {
        ble.major(0);
        mode = 0;
      }
      break;  
  }
}
```

**Собираем по схеме**

![Схема радионяни своими руками](https://github.com/cutecare/cutecare-docs/blob/master/images/BabyMonitor_bb.png?raw=true)

**Программируем HASS**

Файл: /config/configuration.yaml

```
binary_sensor:
  - platform: cutecare
    name: Noise sensor
    mac: 00:15:84:00:6F:40

notify:
  - name: baby_monitor
    platform: pushbullet
    api_key: YOUR_API_KEY
    
alert:
  baby_monitor:
    name: Notify when baby is crying
    entity_id: binary_sensor.noise_sensor
    repeat: 20
    notifiers:
      - baby_monitor
```

Для получения уведомлений необходимо зарегистрироваться в сервисе Pushbullet, 
установить приложение на телефон и в конфигурации HASS значение YOUR_API_KEY заменить на ключ, который можно получить в настройках сервиса.
